plugins {
    id 'org.springframework.boot' version '2.3.4.RELEASE'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
}

apply from: rootProject.rootDir.getAbsolutePath() + "/gradle/ext.gradle"

subprojects {

    apply plugin: 'application'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    apply from: gradlePath + "spring.gradle"

    //JVM 版本
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    [compileJava, compileTestJava, javadoc]*.options*.encoding = "UTF-8"


    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = "sources"
        from sourceSets.main.allSource
    }

    task deleteOut(type: Delete) {
        delete 'out'
    }

    task jarWithoutResources(type: Jar) {
        baseName project.name
        from("$buildDir/classes/main")
    }

    artifacts {
        archives sourcesJar
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    configurations {
        all*.exclude group: "org.slf4j", module: "slf4j-log4j12"
        all*.exclude group: "log4j", module: "log4j"
        all*.exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
        all*.exclude group: "junit", module: "junit"
    }

    test {
        useJUnitPlatform {
            includeTags 'fast', 'smoke & feature-a'
            // excludeTags 'slow', 'ci'
            includeEngines 'junit-jupiter'
            excludeEngines 'junit-vintage'
        }
    }
}

allprojects {
    group "cn.jarod"
    version "1.0-SNAPSHOT"

    apply plugin: 'java'
    apply plugin: 'idea'

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://maven.aliyun.com/repository/spring/"}
        maven { url "https://repo.spring.io/release" }
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
        maven { url 'https://maven.springframework.org/release' }
//        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'https://projectlombok.org/edge-releases' }

    }
}